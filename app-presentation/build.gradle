apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {

    compileSdkVersion androidSdkVersion
    buildToolsVersion androidToolsVersion

    defaultConfig {

        applicationId basePackageName
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidSdkVersion

        versionCode versionCode
        versionName versionName
    }

    buildTypes {

        release {

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {

            applicationIdSuffix '.debug'
        }
    }

    productFlavors.whenObjectAdded { flavor ->

        def flavorData = rootProject.ext[flavor.name]
        flavor.applicationId flavorData.packageName

        flavor.buildConfigField 'boolean', fieldShowAds, flavorData.showAds
        flavor.resValue 'string', appNameResId, flavorData.appName
        flavor.resValue 'color', brandColorResId, flavorData.appColor
    }

    productFlavors {

        paid {}

        free {}
    }

    packagingOptions {

        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')

    def dependencies =
            rootProject.ext.presentationDependencies

    compile dependencies.supportV4
    compile dependencies.appCompat
    compile dependencies.recyclerView
    compile dependencies.butterKnife
    compile dependencies.playServices
    compile dependencies.rxAndroid
    compile dependencies.rxJava
    compile(dependencies.crashlytics) { transitive = true; }

    compile project(':app-domain')
    compile project(':app-model')
}
